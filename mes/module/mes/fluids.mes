;;; -*-scheme-*-

;;; GNU Mes --- Maxwell Equations of Software
;;; Copyright © 2016,2018 Jan (janneke) Nieuwenhuizen <janneke@gnu.org>
;;; Copyright © 2022 Timothy Sample <samplet@ngyro.com>
;;;
;;; This file is part of GNU Mes.
;;;
;;; GNU Mes is free software; you can redistribute it and/or modify it
;;; under the terms of the GNU General Public License as published by
;;; the Free Software Foundation; either version 3 of the License, or (at
;;; your option) any later version.
;;;
;;; GNU Mes is distributed in the hope that it will be useful, but
;;; WITHOUT ANY WARRANTY; without even the implied warranty of
;;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;;; GNU General Public License for more details.
;;;
;;; You should have received a copy of the GNU General Public License
;;; along with GNU Mes.  If not, see <http://www.gnu.org/licenses/>.

;;; Commentary:

;;; Code:

(mes-use-module (mes scm))

;; Hide the fluid table in a lexical binding.
(let ((fluids (make-hash-table)))

  (define (make-fluid . default)
    (let ((fluid (symbol-append 'fld: (gensym))))
      (hashq-set! fluids fluid (and (pair? default) (car default)))
      fluid))

  (define (fluid? fluid)
    (and (hashq-get-handle fluids fluid) #t))

  (define (fluid-ref fluid)
    (let ((handle (hashq-get-handle fluids fluid)))
      (if handle
          (cdr handle)
          (error "invalid fluid" fluid))))

  (define (fluid-set! fluid value)
    (let ((handle (hashq-get-handle fluids fluid)))
      (if handle
          (set-cdr! handle value)
          (error "invalid fluid" fluid))))

  (hashq-set! (initial-module) 'make-fluid (make-variable make-fluid))
  (hashq-set! (initial-module) 'fluid? (make-variable fluid?))
  (hashq-set! (initial-module) 'fluid-ref (make-variable fluid-ref))
  (hashq-set! (initial-module) 'fluid-set! (make-variable fluid-set!)))

(define (with-fluid* fluid value thunk)
  (let ((v (fluid-ref fluid)))
    (fluid-set! fluid value)
    (let ((r (thunk)))
      (fluid-set! fluid v)
      r)))

;; (define-macro (with-fluids*-macro fluids values thunk)
;;   `(begin
;;      ,@(map (lambda (f v) (list 'set! f v)) fluids values)
;;      (,thunk)))

;; (define (with-fluids*-next fluids values thunk)
;;   `(with-fluids*-macro ,fluids ,values ,thunk))

;; (define (with-fluids* fluids values thunk)
;;   (primitive-eval (with-fluids*-next fluids values thunk)))

(define-macro (with-fluids bindings . bodies)
  (let ((syms (map gensym bindings)))
    `(let ,(map (lambda (b s) `(,s (fluid-ref ,b))) (map car bindings) syms)
       ,@(map (lambda (o) `(fluid-set! ,(car o) ,(cadr o))) bindings)
       (let ((r (begin ,@bodies)))
         ,@(map (lambda (b s) `(fluid-set! ,b ,s)) (map car bindings) syms)
         r))))

(define (dynamic-wind in-guard thunk out-guard)
  (in-guard)
  (let ((r (thunk)))
    (out-guard)
    r))
